options {
  JAVA_UNICODE_ESCAPE = true;
  STATIC = false;
  //DEBUG_PARSER = true;
//DEBUG_LOOKAHEAD = true;
}

PARSER_BEGIN(TomParser) 
/*
  
    TOM - To One Matching Compiler

    Copyright (C) 2000-2004 INRIA
			    Nancy, France.

    This program is free software; you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation; either version 2 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program; if not, write to the Free Software
    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307 USA
    
    Pierre-Etienne Moreau	e-mail: Pierre-Etienne.Moreau@loria.fr

*/

package jtom.parser;

import java.io.*;
import java.util.*;
import java.text.*;
import jtom.TomEnvironment;
import jtom.adt.tomsignature.*;
import jtom.adt.tomsignature.types.*;
import jtom.adt.tomsignature.types.instruction.RuleSet;
import jtom.adt.tomsignature.types.instruction.Match;
import jtom.exception.*;
import jtom.tools.*;
import jtom.checker.TomCheckerMessage;
import jtom.xml.Constants;

public class TomParser extends TomTask {
  private int oldPos=0, oldLine=0, includeOffSet=0;
  private Position orgTrack;
  private TomBuffer tomBuffer;
  private TomBackQuoteParser tomBackQuoteParser;
  private TomFactory tomFactory;
  private File importList[];
  private HashSet includedFiles;
  private String currentFile;
  private LinkedList debuggedStructureList;
  private String text="";
  private boolean debugMode = false, xmlMode = false, debugMemory = false, noWarning = false, pretty = false;
  
   public TomParser(TomBuffer input, TomEnvironment environment, File importList[], int includeOffSet, 
  							String fileName)  {
  	  this(input, environment, importList, includeOffSet, fileName, new HashSet());
  }
  
  private TomParser(TomBuffer input, jtom.TomEnvironment environment, File importList[], int includeOffSet, 
  							String fileName, HashSet alreadyParsedFiles) {
    super("Tom Parser", environment);
    jj_input_stream = new JavaCharStream(input, 1, 1);
    token_source = new TomParserTokenManager(jj_input_stream);
    token = new Token();
    jj_ntk = -1;
    jj_gen = 0;
    for (int i = 0; i < 64; i++) jj_la1[i] = -1;
    for (int i = 0; i < jj_2_rtns.length; i++) jj_2_rtns[i] = new JJCalls();
    this.tomBuffer = input;
    this.importList = importList;
    this.includeOffSet = includeOffSet;
    this.orgTrack = makePosition(1,1);
    this.currentFile = fileName;
    this.debuggedStructureList = new LinkedList();
    this.tomBackQuoteParser = new TomBackQuoteParser(environment);
    this.tomFactory = new TomFactory(environment);
    includedFiles = alreadyParsedFiles;
  }
  
  private void testIncludedFiles(String fileName) throws TomIncludeException {
    if(!includedFiles.contains(fileName)) {
      includedFiles.add(fileName);
    } else {
      String msg = "Re-entering included file `"+fileName+"` in `"+currentFile+"` forms a cycle. Breaking the parsing...";
      throw new TomIncludeException(new Throwable(msg));
    }
  }
  
  public void initProcess() {
    debugMode = getInput().isDebugMode();
    debugMemory = getInput().isDebugMemory();
    pretty = getInput().isPretty();
    noWarning = getInput().isNoWarning();
  }
  
  public void process() {
  	try {
  		long startChrono = 0;
  		boolean verbose = getInput().isVerbose(), intermediate = getInput().isIntermediate();
  		if(verbose) { startChrono = System.currentTimeMillis();}
  		
  		TomTerm parsedTerm = startParsing();
  		
  		if(verbose) {
  			System.out.println("TOM parsing phase (" + (System.currentTimeMillis()-startChrono)+ " ms)");
  		}
        if(intermediate) {
            Tools.generateOutput(getInput().getBaseInputFileName() + TomTaskInput.parsedSuffix, parsedTerm);
            Tools.generateOutput(getInput().getBaseInputFileName() + TomTaskInput.parsedTableSuffix, symbolTable().toTerm());
        }
        
        if(getInput().isDebugMode()) {
			Tools.generateOutput(getInput().getBaseInputFileName() + TomTaskInput.debugTableSuffix, getStructTable());	
		}
        
  		// Update taskInput
  		getInput().setTerm(parsedTerm);
  		
  	} catch (TomIncludeException e) {
  	  addError(e.getMessage(), currentFile,  getLine(), TomCheckerMessage.TOM_ERROR);
  	} catch (TomException e) {
  	  addError(e.getMessage(), currentFile,  getLine(), TomCheckerMessage.TOM_ERROR);
  	} catch (ParseException e) {
  	  String msg = "Parsing exception catched in file `"+currentFile+"`\n"+e.getMessage();
  	  addError(e.getMessage(), currentFile, getLine(), TomCheckerMessage.TOM_ERROR);
  	} catch (Exception e) {
  	  String msg = "Unhandled exception occurs during parsing: "+e.getMessage();
          e.printStackTrace();
  	  addError(msg, currentFile, 0, TomCheckerMessage.TOM_ERROR);
  	}
  }
  
  private int getLine() {
    return  token.beginLine+includeOffSet;
  }

  private int getPos() {
    return jj_input_stream.getBeginOffset();
  }

  private String savePosAndExtract() {
    return savePosAndExtract(0);
  }
  
  private String savePosAndExtract(int offset) {
    String string;
    string = tomBuffer.extractBuffer(oldPos,getPos() + offset);
    oldPos = getPos() + offset;
    return string;
  }

  private void putSymbol(String name, TomSymbol symbol) {
    symbolTable().putSymbol(name,symbol);
  }

  private void putType(String name, TomType type) {
    symbolTable().putType(name,type);
  }

  private void switchToTomMode() {
    token_source.SwitchTo(TOM);
  }

  private void switchToDefaultMode() {
    token_source.SwitchTo(DEFAULT);
    oldPos = getPos() + token.image.length();
    oldLine = getLine();
    orgTrack = makePosition(token.beginLine,token.beginColumn);
  }
  
  public Position makePosition(int line, int column) {
    return  tsf().makePosition_TextPosition(line+includeOffSet, column);
  }
  
  private TargetLanguage makeTL(String code) {
    Position newOriginTracking = makePosition(token.beginLine,token.beginColumn);
    return tsf().makeTargetLanguage_TL(code, orgTrack, newOriginTracking); 
  }
  
  public TomStructureTable getStructTable() {
    TomList list = ast().makeList(debuggedStructureList);
    return tsf().makeTomStructureTable_StructTable(list);
  }

  private void addPreviousCode(LinkedList list) {
    String code = savePosAndExtract();
    String pureCode = code.replace('\t', ' ');
    //pureCode = pureCode.replace('\n', ' ');
    if(pureCode.matches("\\s*")) {
      return;
    }
    if (code.equals("")) {return ;}
    list.add(makeTL(code));
  }
}

PARSER_END(TomParser)


/*******************************************
 * THE TOM TOKEN SPECIFICATION STARTS HERE *
 *******************************************/

/*******************************************
 * DEFAULT MODE                            *
 *******************************************/
  
TOKEN :
{
    < MATCH:               "%match" > : TOM
  | < RULE:                "%rule" > : TOM
  | < MAKE_TERM:           "%make" > : TOM
  | < BACKQUOTE_TERM:      "`" > : TOM
  | < VARIABLE:            "%variable" > : TOM
  | < TYPE:                "%type" > : TOM
  | < TYPETERM:            "%typeterm" > : TOM
  | < TYPEINT:             "%typeint" > : TOM
  | < TYPEDOUBLE:          "%typedouble" > : TOM
  | < TYPESTRING:          "%typestring" > : TOM
  | < TYPELIST:            "%typelist" > : TOM
  | < TYPEARRAY:           "%typearray" > : TOM
  | < INCLUDE:             "%include" > : TOM
  | < OPERATOR:            "%op" > : TOM
  | < OPERATOR_LIST:       "%oplist" > : TOM
  | < OPERATOR_ARRAY:      "%oparray" > : TOM
}

/* WHITE SPACE */
SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| "\f"
}

/* COMMENTS */

MORE :
{
  "//" : IN_SINGLE_LINE_COMMENT
| <"/**" ~["/"]> { input_stream.backup(1); } : IN_FORMAL_COMMENT
| "/*" : IN_MULTI_LINE_COMMENT
}

<IN_SINGLE_LINE_COMMENT>
SPECIAL_TOKEN :
{
  <SINGLE_LINE_COMMENT: "\n" | "\r" | "\r\n" > : DEFAULT
}

<IN_FORMAL_COMMENT>
SPECIAL_TOKEN :
{
  <FORMAL_COMMENT: "*/" > : DEFAULT
}

<IN_MULTI_LINE_COMMENT>
SPECIAL_TOKEN :
{
  <MULTI_LINE_COMMENT: "*/" > : DEFAULT
}

<IN_SINGLE_LINE_COMMENT,IN_FORMAL_COMMENT,IN_MULTI_LINE_COMMENT>
MORE :
{
  < ~[] >
}

SPECIAL_TOKEN :
{
 < STRING_LITERAL:
      "\""
      (   (~["\"","\\","\n","\r"])
        | ("\\"
            ( ["n","t","b","r","f","\\","'","\""]
            | ["0"-"7"] ( ["0"-"7"] )?
            | ["0"-"3"] ["0"-"7"] ["0"-"7"]
            )
          )
      )*
      "\""
  >
}

/* SEPARATORS */

TOKEN :
{
  < LBRACE: "{" >
| < RBRACE: "}" >
| < OTHER: ~[] >
}

/************************************************************
 * TOM MODE
 ************************************************************/

/* TOM WHITE SPACE */
<TOM>
SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| "\f"
| <	( " " | "\t" | "\n" | "\r" )+		  >
| <	"<!--" ( ~["-"] | ( "-" ~["-"] ) )* "-->" >
| <	"<?"   (~[">"])* ">"                      >
}

/* TOM COMMENTS */
<TOM>
MORE :
{
  "//" : TOM_IN_SINGLE_LINE_COMMENT
|
  <"/**" ~["/"]> { input_stream.backup(1); } : TOM_IN_FORMAL_COMMENT
|
  "/*" : TOM_IN_MULTI_LINE_COMMENT
}

<TOM_IN_SINGLE_LINE_COMMENT>
SPECIAL_TOKEN :
{
  <TOM_SINGLE_LINE_COMMENT: "\n" | "\r" | "\r\n" > : TOM
}

<TOM_IN_FORMAL_COMMENT>
SPECIAL_TOKEN :
{
  <TOM_FORMAL_COMMENT: "*/" > : TOM
}

<TOM_IN_MULTI_LINE_COMMENT>
SPECIAL_TOKEN :
{
  <TOM_MULTI_LINE_COMMENT: "*/" > : TOM
}

<TOM_IN_SINGLE_LINE_COMMENT,TOM_IN_FORMAL_COMMENT,TOM_IN_MULTI_LINE_COMMENT>
MORE :
{
  < ~[] >
}

/* TOM IDENTIFIERS */
<TOM>
TOKEN :
{
  < TOM_ARROW:       "->" >
| < TOM_DOUBLE_ARROW:"=>" >
| < TOM_ALTERNATIVE: "|" >
| < TOM_COMMA:       "," >
| < TOM_COLON:       ":" >
| < TOM_EQUAL:       "=" >
| < TOM_AT:          "@" >
| < TOM_DOT:         "." >
| < TOM_LPAREN:      "(" >
| < TOM_RPAREN:      ")" >
| < TOM_LBRACKET:    "[" >
| < TOM_RBRACKET:    "]" >
| < TOM_LBRACE:      "{" >
| < TOM_RBRACE:      "}" >
| < TOM_UNDERSCORE:  "_" >
| < TOM_MINUS:       "-" >
| < TOM_PLUS:        "+" >
| < TOM_STAR:        "*" >
| < TOM_SHARP:       "#" >
| < TOM_AND:         "&" >
| < TOM_WHERE:       "where" >
| < TOM_IF:          "if" >
| < TOM_BACKQUOTE:   "`" >
| < TOM_MAKE:        "make" >
| < TOM_MAKE_EMPTY:  "make_empty" >
| < TOM_MAKE_INSERT: "make_insert" >
| < TOM_MAKE_APPEND: "make_append" >
| < TOM_IMPLEMENT:   "implement" >
| < TOM_FSYM:        "fsym" >
| < TOM_GET_SLOT:    "get_slot" >
| < TOM_IS_FSYM:     "is_fsym" >
| < TOM_GET_SUBTERM: "get_subterm" >
| < TOM_GET_FUN_SYM: "get_fun_sym" >
| < TOM_CMP_FUN_SYM: "cmp_fun_sym" >
| < TOM_EQUALS:      "equals" >
| < TOM_GET_HEAD:    "get_head" >
| < TOM_GET_TAIL:    "get_tail" >
| < TOM_IS_EMPTY:    "is_empty" >
| < TOM_GET_ELEMENT: "get_element" >
| < TOM_GET_SIZE:    "get_size" >
| < TOM_INTEGER: <TOM_DIGIT> (<TOM_DIGIT>)* >
| < TOM_IDENTIFIER: <TOM_LETTER> (<TOM_LETTER>|<TOM_DIGIT>|<TOM_UNDERSCORE>|<TOM_MINUS>)* >
| < TOM_DOUBLE:
        (["0"-"9"])+ "." (["0"-"9"])* (<EXPONENT>)? (["f","F","d","D"])?
      | "." (["0"-"9"])+ (<EXPONENT>)? (["f","F","d","D"])?
      | (["0"-"9"])+ <EXPONENT> (["f","F","d","D"])?
      | (["0"-"9"])+ (<EXPONENT>)? ["f","F","d","D"]
  >
| < #EXPONENT: ["e","E"] (["+","-"])? (["0"-"9"])+ >
| < #TOM_LETTER:
      [
       "\u0024",
       "\u0041"-"\u005a",
       "\u005f",
       "\u0061"-"\u007a",
       "\u00c0"-"\u00d6",
       "\u00d8"-"\u00f6",
       "\u00f8"-"\u00ff",
       "\u0100"-"\u1fff",
       "\u3040"-"\u318f",
       "\u3300"-"\u337f",
       "\u3400"-"\u3d2d",
       "\u4e00"-"\u9fff",
       "\uf900"-"\ufaff"
      ]
  >
| < #TOM_DIGIT:
      [
       "\u0030"-"\u0039",
       "\u0660"-"\u0669",
       "\u06f0"-"\u06f9",
       "\u0966"-"\u096f",
       "\u09e6"-"\u09ef",
       "\u0a66"-"\u0a6f",
       "\u0ae6"-"\u0aef",
       "\u0b66"-"\u0b6f",
       "\u0be7"-"\u0bef",
       "\u0c66"-"\u0c6f",
       "\u0ce6"-"\u0cef",
       "\u0d66"-"\u0d6f",
       "\u0e50"-"\u0e59",
       "\u0ed0"-"\u0ed9",
       "\u1040"-"\u1049"
      ]
  >
| < TOM_STRING:
      "\""
      (   (~["\"","\\","\n","\r"])
        | ("\\"
            ( ["n","t","b","r","f","\\","'","\""]
            | ["0"-"7"] ( ["0"-"7"] )?
            | ["0"-"3"] ["0"-"7"] ["0"-"7"]
            )
          )
      )*
      "\""
  >
        
| < XML_NAME: (<TOM_LETTER>|<TOM_DIGIT>)+ (<TOM_LETTER>|<TOM_DIGIT> | "_" )* >
| < XML_START: "<" >
| < XML_START_ENDING: "</" >
| < XML_CLOSE: ">" >
| < XML_CLOSE_SINGLETON: "/>" >
| < DOUBLE_QUOTE: "\"">
| < XML_TEXT: "#TEXT">
| < XML_COMMENT: "#COMMENT">
| < XML_PROC: "#PROCESSING-INSTRUCTION">
}


/*********************************************
 * THE TOM GRAMMAR SPECIFICATION STARTS HERE *
 *********************************************/

TomTerm startParsing() throws TomException : /* in DEFAULT mode */
{
  String upToEOF; 
  TomTerm parseTree = null;
  LinkedList blockList = new LinkedList();
}
{
  BlockList(blockList)
    {
      upToEOF = tomBuffer.extractBuffer(oldPos,getPos());
      blockList.add(makeTL(upToEOF));
      String comment = "Generated by TOM: Do not edit this file";
      blockList.addFirst(tsf().makeTargetLanguage_Comment(comment));
      parseTree = tsf().makeTomTerm_Tom(ast().makeList(blockList));
    }
  <EOF>
    {
      return parseTree;
    } 
}

void BlockList(LinkedList blockList) throws TomException : /* in DEFAULT mode */
{}
{
  (
      MatchConstruct(blockList)
    | RuleConstruct(blockList)
    | BackQuoteTerm(blockList)
    | IncludeConstruct(blockList)
    | LocalVariableConstruct(blockList)
    | Operator(blockList)
    | OperatorList(blockList)
    | OperatorArray(blockList)
    | TypeTerm(blockList)     
    | TypeInt(blockList)      
    | TypeDouble(blockList)   
    | TypeString(blockList)   
    | TypeList(blockList)     
    | TypeArray(blockList)    
    | <LBRACE> BlockList(blockList) <RBRACE>
    | <OTHER>
  )*
}

void MatchConstruct(LinkedList list) throws TomException: /* in DEFAULT mode */
{
  LinkedList matchArgumentsList = new LinkedList();
  LinkedList patternActionList = new LinkedList();
  LinkedList optionList = new LinkedList();
}
{
  <MATCH> /* switch to TOM mode */
    {
      addPreviousCode(list);
      Option orgTrack = ast().makeOriginTracking("Match",getLine(), currentFile);
      optionList.add(orgTrack);
      String debugKey = orgTrack.getFileName().getString() + orgTrack.getLine();
    }

  <TOM_LPAREN> MatchArguments(matchArgumentsList) <TOM_RPAREN>

  <TOM_LBRACE> ( PatternAction(patternActionList, debugKey) )* <TOM_RBRACE> /* we are in TOM mode */
    {
      switchToDefaultMode(); /* switch to DEFAULT mode */
      OptionList option = ast().makeOptionList(optionList);
      Match match = tsf().makeInstruction_Match(tsf().makeTomTerm_SubjectList( ast().makeList(matchArgumentsList)),
                                                tsf().makeTomTerm_PatternList( ast().makeList(patternActionList)),
                                                option);
      list.add(match);
      if (debugMode)
        debuggedStructureList.add(match);
    }
}

void PatternAction(LinkedList list, String debugKey) throws TomException: /* in TOM mode */
{
  LinkedList matchPatternsList = new LinkedList();
  LinkedList listTextPattern = new LinkedList();
  LinkedList listOrgTrackPattern = new LinkedList();
  LinkedList listOfMatchPatternsList = new LinkedList();
  LinkedList blockList = new LinkedList();
  TargetLanguage tlCode;
  Option option;
  Token label = null;
  text = "";
}
{

  [ LOOKAHEAD(2) label=<TOM_IDENTIFIER> <TOM_COLON> ]
  option = MatchPatterns(matchPatternsList)
    { listOfMatchPatternsList.add(ast().makeList(matchPatternsList));
      matchPatternsList.clear();
      listTextPattern.add(text);text = "";
      listOrgTrackPattern.add(option);
    }
  [ ( <TOM_ALTERNATIVE>  MatchPatterns(matchPatternsList) 
    { listOfMatchPatternsList.add(ast().makeList(matchPatternsList));
      matchPatternsList.clear();
      listTextPattern.add(text);text = "";
      listOrgTrackPattern.add(option);
    }
    )+ ]
  <TOM_ARROW>
    {
      if(debugMode) {
        blockList.add(tsf().makeTargetLanguage_ITL("jtom.debug.TomDebugger.debugger.patternSuccess(\""+debugKey+"\");\n"));
        if(debugMemory) {
          blockList.add(tsf().makeTargetLanguage_ITL("jtom.debug.TomDebugger.debugger.emptyStack();\n"));
        }
      }
    }
  tlCode = GoalLanguageBlock(blockList)
    {
      blockList.add(tlCode);
      TomList patterns;
      String patternText = "";
      LinkedList optionList = new LinkedList();
      if(label != null) {
        optionList.add(tsf().makeOption_Label(tsf().makeTomName_Name(label.image)));
      }
      for(int i=0 ;  i<listOfMatchPatternsList.size() ; i++) {
        patterns = (TomList)listOfMatchPatternsList.get(i);
        patternText = (String)listTextPattern.get(i);
          //TODO solve with xmlterm
          //if (patternText == null) patternText = "";
        optionList.add(listOrgTrackPattern.get(i));
        optionList.add(tsf().makeOption_OriginalText(tsf().makeTomName_Name(patternText)));
        list.add(tsf().makeTomTerm_PatternAction(
                   tsf().makeTomTerm_TermList(patterns),
                   tsf().makeInstruction_UnamedBlock(ast().makeInstructionList(blockList)),
                   ast().makeOptionList(optionList)));
      }
    }
}

void MatchArguments(LinkedList list) throws TomException: /* in TOM mode */
{}
{
  MatchArgument(list) ( <TOM_COMMA> MatchArgument(list) )*
}

void MatchArgument(LinkedList list) throws TomException: /* in TOM mode */
{
  Token type,name;
}
{
  type=<TOM_IDENTIFIER> name=<TOM_IDENTIFIER>
  {  	list.add(tsf().makeTomTerm_TLVar(
                   name.image,
                   tsf().makeTomType_TomTypeAlone(type.image)));
  }
}

Option MatchPatterns(LinkedList list) throws TomException: /* in TOM mode */
{
  TomTerm term;
  Option orgTrack;
} 
{
  term=AnnotedTerm()
    {
      list.add(term);
      orgTrack = ast().makeOriginTracking("Pattern",getLine(), currentFile);
    }
    ( <TOM_COMMA> {text += "\n";} term=AnnotedTerm() { list.add(term); } )*
    { return orgTrack;}
}


TomName HeadSymbol(LinkedList optionList)  throws TomException:
{
  Token name;
}
{
    // identifier | integer | double | string
  ( name = <TOM_IDENTIFIER> |
    name = <TOM_INTEGER> |
    name = <TOM_DOUBLE> |
    name = <TOM_STRING>)
    {
      text += name.image;
      optionList.add(ast().makeOriginTracking(name.image,getLine(), currentFile));
      switch(name.kind) {
          case TOM_INTEGER:
            ast().makeIntegerSymbol(symbolTable(),name.image,optionList);
            break;
          case TOM_DOUBLE:
            ast().makeDoubleSymbol(symbolTable(),name.image,optionList);
            break;
          case TOM_STRING:
            ast().makeStringSymbol(symbolTable(),name.image,optionList);
            break;
          default:
      }
      return tsf().makeTomName_Name(name.image);
    }
}

NameList HeadSymbolList(LinkedList optionList)  throws TomException:
{
  TomName name;
  LinkedList nameList = new LinkedList();
}
{
  (
  name = HeadSymbol(optionList) { nameList.add(name); }
  |
    <TOM_LPAREN> { text += "("; }
    name = HeadSymbol(optionList)  { nameList.add(name); }
    <TOM_ALTERNATIVE> { text += "|"; }
    name = HeadSymbol(optionList)  { nameList.add(name); }
    ( <TOM_ALTERNATIVE> { text += "|"; }
      name = HeadSymbol(optionList)  { nameList.add(name); }
    )*
    <TOM_RPAREN> { text += ")"; }
  )
  {
    return ast().makeNameList(nameList);
  }
}

TomTerm UnamedVariableStarOrVariableStar(LinkedList optionList)  throws TomException:
{
  Token name;
  OptionList option;
}
{ // X* | _*
  (name = <TOM_IDENTIFIER> | name = <TOM_UNDERSCORE>) <TOM_STAR>
    {
      text += name.image + "*";
      optionList.add(ast().makeOriginTracking(name.image,getLine(), currentFile));
      option = ast().makeOptionList(optionList);
      if(name.kind == TOM_UNDERSCORE) {
        return ast().makeUnamedVariableStar(option,"unknown type");
      } else {
        return ast().makeVariableStar(option,name.image,"unknown type");
      }
    }
}

TomTerm Placeholder(LinkedList optionList) throws TomException:
{
  OptionList option;
}
{ // _
  <TOM_UNDERSCORE>
    {
      text += "_";
      optionList.add(ast().makeOriginTracking("_",getLine(), currentFile));
      option = ast().makeOptionList(optionList);
      return tsf().makeTomTerm_Placeholder(option);
    }
}

TomTerm PlainTerm(TomName astAnnotedName) throws TomException: /* in TOM mode */
{
  LinkedList list = new LinkedList();
  Token name;
  TomTerm term;
  LinkedList optionList = new LinkedList();
  NameList nameList = null;
  boolean implicit = false;
  boolean withArgs = false;
  Option annotedName = (astAnnotedName==null)?null:tsf().makeOption_TomNameToOption(astAnnotedName);
  if(annotedName!=null) { optionList.add(annotedName); }
}
{
    term = XMLTerm(optionList) { return term; }
  | // X* | _*
    LOOKAHEAD(2)
    term = UnamedVariableStarOrVariableStar(optionList) {return term; }
  | // _
    term = Placeholder(optionList) {return term; }
  | // f(a,...) | f[slot=a,...]
    (
      LOOKAHEAD(3)
      nameList = HeadSymbolList(optionList)
      [ LOOKAHEAD( {getToken(1).kind==TOM_LPAREN} )
        implicit = ExplicitTermList(list) { withArgs = true; }
      | implicit = ImplicitPairList(list) { withArgs = true; } 
      ]
      {
        if(withArgs && list.isEmpty()) {
            // check if it is a constant
          optionList.add(tsf().makeOption_Constructor(nameList));
        }
        if(implicit) {
          return tsf().makeTomTerm_RecordAppl(ast().makeOptionList(optionList),nameList,ast().makeList(list));
        } else {
          return tsf().makeTomTerm_Appl(ast().makeOptionList(optionList),nameList,ast().makeList(list));
        }
      }
    | implicit = ExplicitTermList(list)
      {
        nameList = tsf().makeNameList(tsf().makeTomName_Name(""));
        optionList.add(ast().makeOriginTracking("",getLine(), currentFile));
        return tsf().makeTomTerm_Appl(ast().makeOptionList(optionList),nameList,ast().makeList(list));
      }
    )
      
}

// return true for implicit mode
boolean ImplicitTermList(LinkedList list) throws TomException: /* in TOM mode */
{
  TomTerm term;
}
{
  <TOM_LBRACKET> { text+="["; }
    [
      term = AnnotedTerm() { list.add(term); }
      ( <TOM_COMMA> {text += ",";} term = AnnotedTerm() { list.add(term); } )*
    ]
  <TOM_RBRACKET>
  {
    text+="]";
    return true;
  }
}

boolean ExplicitTermList(LinkedList list) throws TomException: /* in TOM mode */
{
  TomTerm term;
}
{
  <TOM_LPAREN> { text+="("; }
    [
      term = AnnotedTerm() { list.add(term); }
      ( <TOM_COMMA> {text += ",";} term = AnnotedTerm() { list.add(term); } )*
    ]
  <TOM_RPAREN>
  {
    text+=")";
    return false;
  }
}

// This corresponds to the implicit notation
boolean XMLTermList(LinkedList list) throws TomException: /* in TOM mode */
{
  TomTerm term;
}
{
  ( term = AnnotedTerm() { list.add(term); } )*
  { return true; }
}

boolean ImplicitPairList(LinkedList list) throws TomException: /* in TOM mode */
{
  TomTerm term;
}
{
  <TOM_LBRACKET> { text += "["; }
    [ term = PairTerm() { list.add(term); }
      ( <TOM_COMMA> {text += ",";} term = PairTerm() { list.add(term); } )*
    ]
  <TOM_RBRACKET>
  {
    text+="]";
    return true;
  }
}

boolean ExplicitPairList(LinkedList list) throws TomException: /* in TOM mode */
{
  TomTerm term;
}
{
  <TOM_LPAREN> { text += "("; }
    [ term = PairTerm() { list.add(term); }
      ( <TOM_COMMA> {text += ",";} term = PairTerm() { list.add(term); } )*
    ]
  <TOM_RPAREN>
  {
    text+=")";
    return false;
  }
}


TomTerm PairTerm() throws TomException: /* in TOM mode */
{
  Token name;
  TomTerm term;
}
{
  name = <TOM_IDENTIFIER> <TOM_EQUAL> term = AnnotedTerm()
      {
        text += name.image + "=";  
        return tsf().makeTomTerm_PairSlotAppl(tsf().makeTomName_Name(name.image),term);
      }
}


TomTerm AnnotedTerm() throws TomException: /* in TOM mode */
{
  Token annotedName = null;
  TomTerm term;
  TomName astAnnotedName = null;
}
{
    // foo@PlainTerm()
  [
    LOOKAHEAD(2)
    annotedName = <TOM_IDENTIFIER> <TOM_AT>
    {
      text += annotedName.image+"@";
      astAnnotedName = tsf().makeTomName_Name(annotedName.image);
    }
  ]
    term = PlainTerm(astAnnotedName)
    {
      return term;
    }
}

NameList XMLNameList(LinkedList optionList, boolean needOrgTrack) throws TomException:
{
  Token name;
  NameList nameList;
  String XMLName = "";
  int decLine;
}
{
  (
    name=<TOM_IDENTIFIER>
    {
      text += name.image;
      XMLName += name.image;
      decLine = getLine();
      nameList = tsf().makeNameList(tsf().makeTomName_Name(name.image));
    }
   |
    name=<TOM_UNDERSCORE>
    {
      text += name.image;
      XMLName += name.image;
      decLine = getLine();
      nameList = tsf().makeNameList(tsf().makeTomName_Name(name.image));
    }
   |
    <TOM_LPAREN>
    name=<TOM_IDENTIFIER>
    {
      text += name.image;
      XMLName += name.image;
      decLine = getLine();
      nameList = tsf().makeNameList(tsf().makeTomName_Name(name.image));
    }
    ( <TOM_ALTERNATIVE> name=<TOM_IDENTIFIER>
      {
        text += "|" + name.image;
        XMLName += "|" + name.image;
        nameList = (NameList)nameList.append(tsf().makeTomName_Name(name.image));
      }
    )+
    <TOM_RPAREN>
   )
    {
      if(needOrgTrack) {optionList.add(ast().makeOriginTracking(XMLName, decLine, currentFile));}
      return nameList;
    }
}

TomTerm XMLTerm(LinkedList optionList) throws TomException:
{
  TomTerm term;
  TomTerm arg1;
  TomTerm arg2;
  LinkedList list = new LinkedList();
  LinkedList childs = new LinkedList();
  String keyword = "";
  boolean implicit;
  NameList nameList;
  NameList closingNameList;
  OptionList option;
}
{
  (
    // < NODE attributes [ /> | > childs </NODE> ]
    <XML_START> { text += "<"; }
     nameList = XMLNameList(optionList, true)
     implicit = XMLAttributeList(list)
        {
          if(implicit) { optionList.add(tsf().makeOption_ImplicitXMLAttribut()); }
        }
     (
       // case: /> 
       <XML_CLOSE_SINGLETON>
         {
           text += "\\>";
           option =  ast().makeOptionList(optionList);
         }
     | // case: > childs  </NODE>
       <XML_CLOSE>{text+=">";}
       implicit = XMLChilds(childs)
       <XML_START_ENDING> { text += "</"; }
       closingNameList = XMLNameList(optionList, false)
       <XML_CLOSE> { text += ">";}
        {
          if(!nameList.equals(closingNameList)) {
            String found="", expected ="";
          	while(!nameList.isEmpty()) {
          		expected += "|"+nameList.getHead().getString();
          		nameList = nameList.getTail();
          	}
          	while(!closingNameList.isEmpty()) {
          		found += "|"+closingNameList.getHead().getString();
          		closingNameList = closingNameList.getTail();
          	}
            /*throw new TomException("Error on closing XML pattern: expecting `"+ expected.substring(1) +"` but got `"+found.substring(1)+ "` at line "+getLine());
            return null;*/
            // TODO find the orgTrack of the match
            messageError(getLine(),
                     currentFile,
                     "match",
                     getLine(),
                     TomCheckerMessage.MalformedXMLTerm, 
                     new Object[]{expected.substring(1), found.substring(1)},
                     TomCheckerMessage.TOM_ERROR);
          }
          if(implicit) {
              /*
               * Special case
               * when XMLChilds() is reduced to a singleton
               * Appl(...,Name(""),args)
               */
            if(tomFactory.isExplicitTermList(childs)) {
              childs = tomFactory.metaEncodeExplicitTermList(symbolTable(), (TomTerm)childs.getFirst());
            } else {
              optionList.add(tsf().makeOption_ImplicitXMLChild());
            }
          }
          option = ast().makeOptionList(optionList);
          
        }
      ) // end choice
        {
          term =  tsf().makeTomTerm_XMLAppl(
            option,
            nameList,
            ast().makeList(list),
            ast().makeList(childs));
          return term;
        }
    
  // #TEXT(...)
  | <XML_TEXT> <TOM_LPAREN> arg1 = TermStringIdentifier(null) <TOM_RPAREN>
    {
      keyword = Constants.TEXT_NODE;
      list.add(tsf().makeTomTerm_PairSlotAppl(tsf().makeTomName_Name(Constants.SLOT_DATA),arg1));
    }
  | // #COMMENT(...)
    <XML_COMMENT> <TOM_LPAREN> arg1 = TermStringIdentifier(null) <TOM_RPAREN>
    {
      keyword = Constants.COMMENT_NODE;
      list.add(tsf().makeTomTerm_PairSlotAppl(tsf().makeTomName_Name(Constants.SLOT_DATA),arg1));
    }
  | // #PROCESSING-INSTRUCTION(... , ...)
    <XML_PROC> <TOM_LPAREN>
    arg1=TermStringIdentifier(null) <TOM_COMMA> arg2=TermStringIdentifier(null)
    <TOM_RPAREN>
      {
        keyword = Constants.PROCESSING_INSTRUCTION_NODE;
        list.add(tsf().makeTomTerm_PairSlotAppl(tsf().makeTomName_Name(Constants.SLOT_TARGET),arg1));
        list.add(tsf().makeTomTerm_PairSlotAppl(tsf().makeTomName_Name(Constants.SLOT_DATA),arg2));
      }
     )
    {
      optionList.add(ast().makeOriginTracking(keyword,getLine(), currentFile));
      option = ast().makeOptionList(optionList);
      nameList = tsf().makeNameList(tsf().makeTomName_Name(keyword));
      return tsf().makeTomTerm_RecordAppl(option,
                                    nameList,
                                    ast().makeList(list));

    }
}

  
TomTerm TermStringIdentifier(LinkedList options) throws TomException:
{
  boolean string = false;
  LinkedList optionList = (options==null)?new LinkedList():options;
  OptionList option;
  Token name;
  NameList nameList;
}
{
  ( name = <TOM_IDENTIFIER> | name = <TOM_STRING> { string = true; } )
    {
      text += name.image;
      optionList.add(ast().makeOriginTracking(name.image,getLine(), currentFile));
      option = ast().makeOptionList(optionList);
      if(string) {
        ast().makeStringSymbol(symbolTable(),name.image,optionList);
      }
      nameList = tsf().makeNameList(tsf().makeTomName_Name(name.image));
      return tsf().makeTomTerm_Appl(
        option,
        nameList,
        tsf().makeTomList());
    }
}

boolean XMLChilds(LinkedList list) throws TomException:
{
  LinkedList childs = new LinkedList();
  Iterator it;
  boolean implicit;
}
{
  (LOOKAHEAD(2)
   implicit = ImplicitTermList(childs)
     // | implicit = ExplicitTermList(childs)
     | implicit = XMLTermList(childs)
   )
    {
      it = childs.iterator();
      while(it.hasNext()) {
        list.add(tomFactory.metaEncodeXMLAppl(symbolTable(),(TomTerm)it.next()));
      }
      return implicit;
    }
}

boolean XMLAttributeList(LinkedList list) throws TomException: /* in TOM mode */
{
  Token name;
  TomTerm term;
}
{
  <TOM_LBRACKET> { text += "["; }
    [ term = XMLAttribute() { list.add(term); }
      ( <TOM_COMMA> {text += ",";} term = XMLAttribute() { list.add(term); } )*
    ]
  <TOM_RBRACKET>
  {
    text+="]";
    return true;
  }
  |
  <TOM_LPAREN> { text += "("; }
    [ term = XMLAttribute() { list.add(term); }
      ( <TOM_COMMA> {text += ",";} term = XMLAttribute() { list.add(term); } )*
    ]
  <TOM_RPAREN>
  {
    text+=")";
    return false;
  }
    | // XML notation is implicit
  ( term = XMLAttribute() { list.add(term); } )*
  { return true; }
}

TomTerm XMLAttribute() throws TomException:
{
  LinkedList list = new LinkedList();
  TomTerm term;
  TomTerm termName = null;
  Token id,anno1,anno2;
  String name;
  OptionList option;
  LinkedList optionList = new LinkedList();
  LinkedList optionListAnno2 = new LinkedList();
  NameList nameList;
}
{
    // _* | X*
  LOOKAHEAD(2)
  term = UnamedVariableStarOrVariableStar(optionList) { return term; }
  |
  ( // name = [anno2@](String|Identifier)
    LOOKAHEAD(2)
    id = <TOM_IDENTIFIER> <TOM_EQUAL> {text+=id.image+"=";}
    [
      LOOKAHEAD(2)
    anno2 = <TOM_IDENTIFIER> <TOM_AT>
    {
      text += anno2.image + "@";
      optionListAnno2.add(tsf().makeTomName_Name(anno2.image));
    }
    ]
    term=TermStringIdentifier(optionListAnno2)
      {
        name = tomFactory.encodeXMLString(symbolTable(),id.image);
        nameList = tsf().makeNameList(tsf().makeTomName_Name(name));
        termName = tsf().makeTomTerm_Appl(ast().makeOption(),nameList,tsf().makeTomList());
      }
  | // [anno1@]_ = [anno2@](String|Identifier)
    [ 
    anno1 = <TOM_IDENTIFIER> <TOM_AT>
    {
      text += anno1.image + "@";
      optionList.add(tsf().makeTomName_Name(anno1.image));
    }
    ]
    termName = Placeholder(optionList) <TOM_EQUAL> {text+="=";}
    [
      LOOKAHEAD(2)
    anno2 = <TOM_IDENTIFIER> <TOM_AT>
    {
      text += anno2.image + "@";
      optionListAnno2.add(tsf().makeTomName_Name(anno2.image));
    }
    ]
    term=TermStringIdentifier(optionListAnno2)
  ) 
    {
      list.add(tsf().makeTomTerm_PairSlotAppl(tsf().makeTomName_Name(Constants.SLOT_NAME),termName));
        // we add the specif value : _
      list.add(tsf().makeTomTerm_PairSlotAppl(tsf().makeTomName_Name(Constants.SLOT_SPECIFIED),tsf().makeTomTerm_Placeholder(ast().makeOption())));
        //list.add(tomFactory.metaEncodeXMLAppl(symbolTable(),term));
        // no longer necessary ot metaEncode Strings in attributes
      list.add(tsf().makeTomTerm_PairSlotAppl(tsf().makeTomName_Name(Constants.SLOT_VALUE),term));
      optionList.add(ast().makeOriginTracking(Constants.ATTRIBUTE_NODE,getLine(), currentFile));
      option = ast().makeOptionList(optionList);
      
      nameList = tsf().makeNameList(tsf().makeTomName_Name(Constants.ATTRIBUTE_NODE));
      return tsf().makeTomTerm_RecordAppl(option,
                                    nameList,
                                    ast().makeList(list));
    }
}

void BackQuoteTerm(LinkedList list) throws TomException: /* in DEFAULT mode */
{
  TomTerm term;
  Option orgTrack;
  Token tk;
  int parenLevel = 0, lastTokenLine = 0, firstTokenLine = 0;
  boolean backQuoteMode = false;
  LinkedList tokenList = new LinkedList();
  Token backQuote = null;
  HashMap tokenToDecLineMap = new HashMap();
}
{
  <BACKQUOTE_TERM> /* switch to TOM mode */
  { addPreviousCode(list); }
     [ backQuote = <TOM_BACKQUOTE> ]
    {
      firstTokenLine = getLine();
      orgTrack = ast().makeOriginTracking("Backquote",getLine(), currentFile);
      backQuoteMode = true;
      while(backQuoteMode) {
        try {
          tk = getNextToken();
        } catch (jtom.parser.TokenMgrError Error) {
          throw new TomException("Invalide backquote term started line "+firstTokenLine+": EOF encountered ");
        }
        tokenList.add(tk);
        if(tk.kind == TOM_LPAREN) { 
          parenLevel++;
        } else if(tk.kind == TOM_RPAREN) {
          parenLevel--;
          if(parenLevel==0) {
            backQuoteMode = false;
          }
        } else if (tk.kind == TOM_IDENTIFIER) {
          tokenToDecLineMap.put(tk, new Integer(getLine()));
        }
        lastTokenLine = getLine();
      }
      switchToDefaultMode(); /* switch to DEFAULT mode */
      //term = ast().buildBackQuoteTerm(tokenList);
      term = tomBackQuoteParser.buildBackQuoteTerm(tokenList, tokenToDecLineMap, currentFile);
      
      if(term.isComposite() && term.getArgs().isSingle()) {
        TomTerm backQuoteTerm = term.getArgs().getHead(); 
        if(backQuoteTerm.getAstName().getString().equals("xml")) {
          TomList args = backQuoteTerm.getArgs();
          term = tsf().makeTomTerm_DoubleBackQuote(args);
        } 
      } 
      list.add(term);
    }
 }

void LocalVariableConstruct(LinkedList list) throws TomException: /* in DEFAULT mode */
{}
{
  <VARIABLE> /* switch to TOM mode */
  {
    addPreviousCode(list);
    switchToDefaultMode(); /* switch to DEFAULT mode */
    list.add(tsf().makeInstruction_LocalVariable());
  }
}

void IncludeConstruct(LinkedList list) throws TomException, TomIncludeException : /* in DEFAULT mode */
{
  String fileName;
  TomTerm astTom;
  InputStream input;
  File file;
  byte inputBuffer[];
  TomParser tomParser;
  LinkedList blockList = new LinkedList();
  TargetLanguage tlCode;
}
{
  <INCLUDE> /* switch to TOM mode */
    {
      addPreviousCode(list);
    }
  tlCode = GoalLanguageBlock(blockList)
    {
      switchToDefaultMode(); /* switch to DEFAULT mode */
      fileName = tlCode.getCode().trim();
      fileName = fileName.replace('/',File.separatorChar);
      try {
        file = new File(fileName);
        if(!file.exists()) {
          boolean found = false;
          for(int i=0 ; !found && i<importList.length ; i++) {
            file = new File(importList[i],fileName);
            found = file.exists();
          }
        }
        
          // to get the length of the file
        inputBuffer = new byte[(int)file.length()+1];
        input       = new FileInputStream(file);
        input.read(inputBuffer);
        
        tomParser   = new TomParser(new TomBuffer(inputBuffer),environment(),importList, 0, fileName, includedFiles);
        tomParser.testIncludedFiles(fileName);
        astTom = tomParser.startParsing();
        astTom = tsf().makeTomTerm_TomInclude(astTom.getTomList());
        list.add(astTom);
      } catch (FileNotFoundException e1) {
        String msg = "Included file `" + fileName + "` not found at line "+getLine()+" from file `"+currentFile+"`";
        throw new TomIncludeException(msg);
      }  catch (java.io.IOException e2) {
        String msg = "IOException occurs reading " + fileName + " at line "+getLine();
        System.out.println(msg);
        throw new TomException(msg);
      }
    }
}

void RuleConstruct(LinkedList list) throws TomException: /* in DEFAULT mode */
{
  TomTerm lhs, rhs;
  TomTerm pattern, subject;
  TomRuleList ruleList = tsf().makeTomRuleList();
  LinkedList listOfLhs = new LinkedList();
  LinkedList condList = new LinkedList();
  LinkedList nameTypeInRule = new LinkedList();
  Option orgTrackRuleSet;
  TomName orgText;
}
{
  <RULE> /* switch to TOM mode */
    {
      addPreviousCode(list);
      orgTrackRuleSet = ast().makeOriginTracking("Rule",getLine(), currentFile);
      text = "";
    }
  <TOM_LBRACE> 
  (
    lhs = AnnotedTerm() { listOfLhs.add(lhs); }
    [ ( <TOM_ALTERNATIVE> {text += " | ";} lhs = AnnotedTerm() { listOfLhs.add(lhs); } )+ ]
    <TOM_ARROW> {orgText = tsf().makeTomName_Name(text);} rhs = PlainTerm(null)
    [ (
      <TOM_WHERE> pattern = AnnotedTerm() <TOM_COLON> <TOM_EQUAL> subject = AnnotedTerm()
      { condList.add(tsf().makeInstruction_MatchingCondition(pattern,subject)); }
    | <TOM_IF> pattern = AnnotedTerm() <TOM_EQUAL> <TOM_EQUAL> subject = AnnotedTerm()
      { condList.add(tsf().makeInstruction_EqualityCondition(pattern,subject)); }
    )+ ]
    
    {
      Option orgTrack = ast().makeOriginTracking("Pattern",getLine(), currentFile);
      LinkedList optionList = new LinkedList();
      optionList.add(orgTrack);
      optionList.add(tsf().makeOption_OriginalText(orgText));
      for(int i=0 ; i<listOfLhs.size() ; i++) {
        TomTerm term = (TomTerm) listOfLhs.get(i);
        ruleList = (TomRuleList) ruleList.append(
          tsf().makeTomRule_RewriteRule(
            tsf().makeTomTerm_Term(term),
            tsf().makeTomTerm_Term(rhs),
            ast().makeInstructionList(condList),
            ast().makeOptionList(optionList)));
      }
      listOfLhs.clear();
      condList.clear();
      text="";
    }
  )*
    <TOM_RBRACE>
  {
    switchToDefaultMode(); /* switch to DEFAULT mode */
    RuleSet rule = tsf().makeInstruction_RuleSet(ruleList, orgTrackRuleSet);
    list.add(rule);
    if (debugMode)
      debuggedStructureList.add(rule);
  }
}

/*
 * Operator Declaration
 *
 * in DEFAULT mode
 */

void Operator(LinkedList list) throws TomException : /* in DEFAULT mode */
{
  Token type, name, typeArg, slotName;
  LinkedList blockList = new LinkedList();
  TomTypeList types = tsf().makeTomTypeList();
  LinkedList options = new LinkedList();
  LinkedList slotNameList = new LinkedList();
  Map mapNameDecl = new HashMap();
  TomName astName;
  TomSymbol astSymbol;
  String stringSlotName;
  TargetLanguage tlFsym;
  Declaration attribute;
  TomType tomType;
  SlotList slotList = tsf().makeSlotList();
  Option orgTrack;
}
{
  <OPERATOR> /* switch to TOM mode */
    {
      addPreviousCode(list);
    }
  
  type = <TOM_IDENTIFIER>
  name = <TOM_IDENTIFIER>
    {   orgTrack = ast().makeOriginTracking(name.image,getLine(), currentFile);
    	options.add(orgTrack);
    }
  [ <TOM_LPAREN>
      { stringSlotName = ""; }
      [ LOOKAHEAD(2)
        slotName = <TOM_IDENTIFIER> <TOM_COLON> { stringSlotName = slotName.image; }
      ]
      typeArg = <TOM_IDENTIFIER>
      {
        slotNameList.add(ast().makeName(stringSlotName)); 
        types = (TomTypeList) types.append(tsf().makeTomType_TomTypeAlone(typeArg.image));
      }
      ( <TOM_COMMA>
        { stringSlotName = ""; }
        [ LOOKAHEAD(2)
          slotName = <TOM_IDENTIFIER> <TOM_COLON> { stringSlotName = slotName.image; }
        ]
        typeArg = <TOM_IDENTIFIER>
        {
          astName = ast().makeName(stringSlotName);
          if (!stringSlotName.equals("")) {
            if(slotNameList.indexOf(astName) != -1) {
              String detailedMsg = MessageFormat.format(TomCheckerMessage.RepeatedSlotName, new Object[]{stringSlotName});
              String msg = MessageFormat.format(TomCheckerMessage.MainErrorMessage, new Object[]{new Integer(orgTrack.getLine()), "%op "+type.image, new Integer(orgTrack.getLine()), currentFile, detailedMsg});
              throw new TomException(msg);
            }
          }
          slotNameList.add(astName);
	      types = (TomTypeList) types.append(tsf().makeTomType_TomTypeAlone(typeArg.image));
        }
      )*
      <TOM_RPAREN> ]

  <TOM_LBRACE>
    tlFsym = KeywordFsym()
    {
      astName   = tsf().makeTomName_Name(name.image);
    }
    ( 
      attribute = KeywordMake(name.image,tsf().makeTomType_TomTypeAlone(type.image),types)  { options.add(attribute); }
    | attribute = KeywordGetSlot(astName, type.image)
      {
        TomName sName = attribute.getSlotName();
        if (mapNameDecl.get(sName)==null) {
          mapNameDecl.put(sName,attribute);
        }
        else {
          messageError(attribute.getOrgTrack().getLine(),
                     currentFile,
                     "%op "+type.image,
                     orgTrack.getLine(),
                     TomCheckerMessage.WarningTwoSameSlotDecl, 
                     new Object[]{sName.getString()},
                     TomCheckerMessage.TOM_WARNING
                     );
        }
      }
    | attribute = KeywordIsFsym(astName, type.image)  { options.add(attribute); }
    )*
  <TOM_RBRACE>
    {
      switchToDefaultMode(); /* switch to DEFAULT mode */

      for(int i=slotNameList.size()-1; i>=0 ; i--) {
        TomName name1 = (TomName)slotNameList.get(i);
        PairNameDecl pair = null;
        Declaration emptyDeclaration = tsf().makeDeclaration_EmptyDeclaration();
        if(name1.isEmptyName()) {
          pair = tsf().makePairNameDecl_Slot(name1,emptyDeclaration);
        } else {
          Declaration decl = (Declaration)mapNameDecl.get(name1);
          if(decl == null) {
             messageError(orgTrack.getLine(), 
                         currentFile,
                         "%op "+type.image, 
                         orgTrack.getLine(), 
                         TomCheckerMessage.WarningMissingSlotDecl,
                         new Object[]{name1.getString()},
                         TomCheckerMessage.TOM_WARNING
                         );
            decl = emptyDeclaration;
          }
          else {
            mapNameDecl.remove(name1);
          }
          pair = tsf().makePairNameDecl_Slot(name1,decl);
        }
        slotList = tsf().makeSlotList(pair,slotList);
      }
        // Test if there are still declaration in mapNameDecl
      if ( !mapNameDecl.isEmpty()) {
        Iterator it = mapNameDecl.keySet().iterator();
        while(it.hasNext()) {
           TomName remainingSlot = (TomName) it.next();
           messageError(((Declaration)mapNameDecl.get(remainingSlot)).getOrgTrack().getLine(),
                     currentFile,
                     "%op "+type.image,
                     orgTrack.getLine(),
                     TomCheckerMessage.WarningIncompatibleSlotDecl, new Object[]{remainingSlot.getString()},
                     TomCheckerMessage.TOM_WARNING);
        }
      }
      
      astSymbol = ast().makeSymbol(name.image, type.image, types, slotList, options, tlFsym);
      list.add(tsf().makeDeclaration_SymbolDecl(astName));
      putSymbol(name.image,astSymbol);
    }
}

void OperatorList(LinkedList list) throws TomException: /* in DEFAULT mode */
{
  Token type, name, typeArg;
  LinkedList blockList = new LinkedList();
  TomTypeList types = tsf().makeTomTypeList();
  SlotList slotList = tsf().makeSlotList();
  LinkedList options = new LinkedList();
  TomSymbol astSymbol;
  TomName astName;
  TargetLanguage tlFsym;
  Declaration attribute;  
}
{
  <OPERATOR_LIST> /* switch to TOM mode */
    {
      addPreviousCode(list);
    }
  type = <TOM_IDENTIFIER> name = <TOM_IDENTIFIER>
    { options.add(ast().makeOriginTracking(name.image,getLine(), currentFile));}
         <TOM_LPAREN> typeArg = <TOM_IDENTIFIER> <TOM_STAR> <TOM_RPAREN>
    {
      types = (TomTypeList) types.append(tsf().makeTomType_TomTypeAlone(typeArg.image));
    }
  <TOM_LBRACE>
    tlFsym = KeywordFsym()
    ( 
      attribute = KeywordMakeEmptyList(name.image)                             { options.add(attribute); }
    | attribute = KeywordMakeAddList(name.image, type.image, typeArg.image)    { options.add(attribute); }
    | attribute = KeywordIsFsym(tsf().makeTomName_Name(name.image), type.image)  { options.add(attribute); }
    )*
  <TOM_RBRACE>
    {
      switchToDefaultMode(); /* switch to DEFAULT mode */
      astName   = tsf().makeTomName_Name(name.image);
      slotList =tsf().makeSlotList(tsf().makePairNameDecl_Slot(tsf().makeTomName_EmptyName(), tsf().makeDeclaration_EmptyDeclaration()), slotList);
      astSymbol = ast().makeSymbol(name.image, type.image, types, slotList, options, tlFsym);
      list.add(tsf().makeDeclaration_ListSymbolDecl(astName));
      putSymbol(name.image,astSymbol);
    }
}

void OperatorArray(LinkedList list) throws TomException: /* in DEFAULT mode */
{
  Token type, name, typeArg;
  LinkedList blockList = new LinkedList();
  TomTypeList types = tsf().makeTomTypeList();
  SlotList slotList = tsf().makeSlotList();
  LinkedList options = new LinkedList();
  TomName astName;
  TomSymbol astSymbol;
  TargetLanguage tlFsym;
  Declaration attribute;
}
{
  <OPERATOR_ARRAY> /* switch to TOM mode */
    {
      addPreviousCode(list);
    }
  type = <TOM_IDENTIFIER> name = <TOM_IDENTIFIER>
    { options.add(ast().makeOriginTracking(name.image,getLine(), currentFile));}
         <TOM_LPAREN> typeArg = <TOM_IDENTIFIER> <TOM_STAR> <TOM_RPAREN>
         { 
 	   types = (TomTypeList) types.append(tsf().makeTomType_TomTypeAlone(typeArg.image));
         }
  <TOM_LBRACE>
    tlFsym = KeywordFsym()
    ( 
      attribute = KeywordMakeEmptyArray(name.image, type.image)              { options.add(attribute); }
    | attribute = KeywordMakeAddArray(name.image, type.image, typeArg.image) { options.add(attribute); }
    | attribute = KeywordIsFsym(tsf().makeTomName_Name(name.image), type.image)  { options.add(attribute); }
    )*
  <TOM_RBRACE>
    {
      switchToDefaultMode(); /* switch to DEFAULT mode */
      astName   = tsf().makeTomName_Name(name.image);
      slotList =tsf().makeSlotList(tsf().makePairNameDecl_Slot(tsf().makeTomName_EmptyName(), tsf().makeDeclaration_EmptyDeclaration()), slotList);
      astSymbol = ast().makeSymbol(name.image, type.image, types, slotList, options, tlFsym);
      list.add(tsf().makeDeclaration_ArraySymbolDecl(astName));
      putSymbol(name.image,astSymbol);
    }
}

/*
 * Type Declaration
 *
 * in DEFAULT mode
 */

void TypeTerm(LinkedList list) throws TomException: /* in DEFAULT mode */
{
  Token type;
  LinkedList blockList = new LinkedList();
  TargetLanguage implement;
  Declaration attribute;
  TomType astType;
  Option orgTrack;
  TomName name;
}
{
  (
    <TYPETERM> /* switch to TOM mode */
    {
      addPreviousCode(list);
    }
  |
    <TYPE> /* switch to TOM mode */ 
    {
      addPreviousCode(list);//TODO to remove
    }
  )
  type=<TOM_IDENTIFIER>
    { orgTrack = ast().makeOriginTracking(type.image,getLine(), currentFile);}
  <TOM_LBRACE>
    implement = KeywordImplement()
    ( 
      attribute = KeywordGetFunSym(type.image)  { blockList.add(attribute); }
    | attribute = KeywordGetSubterm(type.image) { blockList.add(attribute); }
    | attribute = KeywordCmpFunSym(type.image)  { blockList.add(attribute); }
    | attribute = KeywordEquals(type.image)     { blockList.add(attribute); }
    )*
  <TOM_RBRACE>
    {
      switchToDefaultMode(); /* switch to DEFAULT mode */
      astType = ast().makeType(type.image,implement);
      name   = tsf().makeTomName_Name(type.image);
      putType(type.image,astType);
      list.add(tsf().makeDeclaration_TypeTermDecl(name, ast().makeList(blockList), orgTrack));
    }
}

void TypeInt(LinkedList list) throws TomException: /* in DEFAULT mode */
{}
{
  <TYPEINT> /* switch to TOM mode */
    {
      addPreviousCode(list);
      switchToDefaultMode(); /* switch to DEFAULT mode */
      ast().makeIntegerDecl(list);
    }
}

void TypeDouble(LinkedList list) throws TomException: /* in DEFAULT mode */
{}
{
  <TYPEDOUBLE> /* switch to TOM mode */
    {
      addPreviousCode(list);
      switchToDefaultMode(); /* switch to DEFAULT mode */
      ast().makeDoubleDecl(list);
    }
}

void TypeString(LinkedList list) throws TomException: /* in DEFAULT mode */
{}
{
  <TYPESTRING> /* switch to TOM mode */
    {
      addPreviousCode(list);
      switchToDefaultMode(); /* switch to DEFAULT mode */
      ast().makeStringDecl(list);
    }
}

void TypeList(LinkedList list) throws TomException: /* in DEFAULT mode */
{
  Token type;
  LinkedList blockList = new LinkedList();
  TargetLanguage implement;
  Declaration attribute;
  TomType astType;
  Option orgTrack;
  TomName name;
}
{
  <TYPELIST> /* switch to TOM mode */
    {
      addPreviousCode(list);
    }
  
  type=<TOM_IDENTIFIER>
    { orgTrack = ast().makeOriginTracking(type.image,getLine(), currentFile); }
  <TOM_LBRACE>
    implement = KeywordImplement()
    ( 
      attribute = KeywordGetFunSym(type.image)  { blockList.add(attribute); }
    | attribute = KeywordGetSubterm(type.image) { blockList.add(attribute); }
    | attribute = KeywordCmpFunSym(type.image)  { blockList.add(attribute); }
    | attribute = KeywordEquals(type.image)     { blockList.add(attribute); }
    | attribute = KeywordGetHead(type.image)    { blockList.add(attribute); }
    | attribute = KeywordGetTail(type.image)    { blockList.add(attribute); }
    | attribute = KeywordIsEmpty(type.image)    { blockList.add(attribute); }
    )*
  <TOM_RBRACE>
    {
      switchToDefaultMode(); /* switch to DEFAULT mode */
      astType = ast().makeType(type.image,implement);
      name   = tsf().makeTomName_Name(type.image);
      putType(type.image,astType);
      list.add(tsf().makeDeclaration_TypeListDecl(name, ast().makeList(blockList), orgTrack));
    }
}

void TypeArray(LinkedList list) throws TomException: /* in DEFAULT mode */
{
  Token type;
  LinkedList blockList = new LinkedList();
  TargetLanguage implement;
  Declaration attribute;
  TomType astType;
  Option orgTrack;
  TomName name;
}
{
  <TYPEARRAY> /* switch to TOM mode */
    {
      addPreviousCode(list);
    }
  
  type=<TOM_IDENTIFIER>
    {orgTrack = ast().makeOriginTracking(type.image,getLine(), currentFile);}
  <TOM_LBRACE>
    implement = KeywordImplement()
    ( 
      attribute = KeywordGetFunSym(type.image)  { blockList.add(attribute); }
    | attribute = KeywordGetSubterm(type.image) { blockList.add(attribute); }
    | attribute = KeywordCmpFunSym(type.image)  { blockList.add(attribute); }
    | attribute = KeywordEquals(type.image)     { blockList.add(attribute); }
    | attribute = KeywordGetElement(type.image) { blockList.add(attribute); }
    | attribute = KeywordGetSize(type.image)    { blockList.add(attribute); }
    )*
  <TOM_RBRACE>
    {
      switchToDefaultMode(); /* switch to DEFAULT mode */
      astType = ast().makeType(type.image,implement);
      putType(type.image,astType);
      name   = tsf().makeTomName_Name(type.image);
      list.add(tsf().makeDeclaration_TypeArrayDecl(name, ast().makeList(blockList), orgTrack));
    }
}

/*
 * in TOM mode
 */

TargetLanguage GoalLanguageBlock(LinkedList blockList) throws TomException:
{
}
{
  <TOM_LBRACE> /* we are in TOM mode */
    {
      switchToDefaultMode(); /* switch to DEFAULT mode */
    }
  BlockList(blockList)
    <RBRACE> /* we are in DEFAULT mode */
    {
      switchToTomMode(); /* switch to TOM mode */
      TargetLanguage tlb = makeTL(savePosAndExtract());
      return tlb;
    }
}

//------------------------------------------------------------

TargetLanguage KeywordImplement() throws TomException:
{
  LinkedList blockList = new LinkedList();
  TargetLanguage tlCode;
}
{
  <TOM_IMPLEMENT>
  tlCode = GoalLanguageBlock(blockList)
   {
     return ast().reworkTLCode(tlCode, pretty);
   }
}

Declaration KeywordGetFunSym(String typeString) throws TomException:
{
  Token name;
  LinkedList blockList = new LinkedList();
  TargetLanguage tlCode;
  Option orgTrack;
}
{
  <TOM_GET_FUN_SYM>
    { orgTrack = ast().makeOriginTracking("get_fun_sym", getLine(), currentFile);}
  <TOM_LPAREN> name = <TOM_IDENTIFIER> <TOM_RPAREN>
  tlCode = GoalLanguageBlock(blockList)
   {
     Option info = ast().makeOriginTracking(name.image,getLine(), currentFile);
     OptionList option = ast().makeOption(info);
     return tsf().makeDeclaration_GetFunctionSymbolDecl(
                           ast().makeVariable(option,name.image,typeString),
                           ast().reworkTLCode(tlCode, pretty), orgTrack);
   }
}

Declaration KeywordGetSubterm(String typeString) throws TomException:
{
  Token name1, name2;
  LinkedList blockList = new LinkedList();
  TargetLanguage tlCode;
  Option orgTrack;
}
{
  <TOM_GET_SUBTERM>
     { orgTrack = ast().makeOriginTracking("get_subterm", getLine(), currentFile);}
  <TOM_LPAREN> name1 = <TOM_IDENTIFIER> <TOM_COMMA> name2 = <TOM_IDENTIFIER> <TOM_RPAREN>
  tlCode = GoalLanguageBlock(blockList)
   {
     Option info1 = ast().makeOriginTracking(name1.image,getLine(), currentFile);
     Option info2 = ast().makeOriginTracking(name2.image,getLine(), currentFile);
     OptionList option1 = ast().makeOption(info1);
     OptionList option2 = ast().makeOption(info2);
     return tsf().makeDeclaration_GetSubtermDecl(
                           ast().makeVariable(option1,name1.image,typeString),
                           ast().makeVariable(option2,name2.image,"int"),
                           ast().reworkTLCode(tlCode, pretty), orgTrack);
   }
}

Declaration KeywordCmpFunSym(String typeString) throws TomException:
{
  Token name1, name2;
  LinkedList blockList = new LinkedList();
  TargetLanguage tlCode;
  Option orgTrack;
}
{
  <TOM_CMP_FUN_SYM>
    { orgTrack = ast().makeOriginTracking("cmp_fun_sym", getLine(), currentFile);}
  <TOM_LPAREN> name1 = <TOM_IDENTIFIER> <TOM_COMMA> name2 = <TOM_IDENTIFIER> <TOM_RPAREN>
  tlCode = GoalLanguageBlock(blockList)
   {
     Option info1 = ast().makeOriginTracking(name1.image,getLine(), currentFile);
     Option info2 = ast().makeOriginTracking(name2.image,getLine(), currentFile);
     OptionList option1 = ast().makeOption(info1);
     OptionList option2 = ast().makeOption(info2);
     return tsf().makeDeclaration_CompareFunctionSymbolDecl(
                           ast().makeVariable(option1,name1.image,typeString),
                           ast().makeVariable(option2,name2.image,typeString),
                           ast().reworkTLCode(tlCode, pretty), orgTrack);
   }
}

Declaration KeywordEquals(String typeString) throws TomException:
{
  Token name1, name2;
  LinkedList blockList = new LinkedList();
  TargetLanguage tlCode;
  Option orgTrack;
}
{
  <TOM_EQUALS>
    { orgTrack = ast().makeOriginTracking("equals", getLine(), currentFile);}
  <TOM_LPAREN> name1 = <TOM_IDENTIFIER> <TOM_COMMA> name2 = <TOM_IDENTIFIER> <TOM_RPAREN>
  tlCode = GoalLanguageBlock(blockList)
   {
     Option info1 = ast().makeOriginTracking(name1.image,getLine(), currentFile);
     Option info2 = ast().makeOriginTracking(name2.image,getLine(), currentFile);
     OptionList option1 = ast().makeOption(info1);
     OptionList option2 = ast().makeOption(info2);
     return tsf().makeDeclaration_TermsEqualDecl(
                           ast().makeVariable(option1,name1.image,typeString),
                           ast().makeVariable(option2,name2.image,typeString),
                           ast().reworkTLCode(tlCode, pretty), orgTrack);
   }
}

Declaration KeywordGetHead(String typeString) throws TomException:
{
  Token name;
  LinkedList blockList = new LinkedList();
  TargetLanguage tlCode;
  Option orgTrack;
}
{
  <TOM_GET_HEAD>
    { orgTrack = ast().makeOriginTracking("get_head", getLine(), currentFile);}
  <TOM_LPAREN> name = <TOM_IDENTIFIER> <TOM_RPAREN>
  tlCode = GoalLanguageBlock(blockList)
   {
     Option info = ast().makeOriginTracking(name.image,getLine(), currentFile);
     OptionList option = ast().makeOption(info);
     return tsf().makeDeclaration_GetHeadDecl(
                           ast().makeVariable(option,name.image,typeString),
                           ast().reworkTLCode(tlCode, pretty), orgTrack);
   }
}

Declaration KeywordGetTail(String typeString) throws TomException:
{
  Token name;
  LinkedList blockList = new LinkedList();
  TargetLanguage tlCode;
  Option orgTrack;
}
{
  <TOM_GET_TAIL>
    { orgTrack = ast().makeOriginTracking("get_tail", getLine(), currentFile);}
  <TOM_LPAREN> name = <TOM_IDENTIFIER> <TOM_RPAREN>
  tlCode = GoalLanguageBlock(blockList)
   {
     Option info = ast().makeOriginTracking(name.image,getLine(), currentFile);
     OptionList option = ast().makeOption(info);
     return tsf().makeDeclaration_GetTailDecl(
                           ast().makeVariable(option,name.image,typeString),
                           ast().reworkTLCode(tlCode, pretty), orgTrack);
   }
}

Declaration KeywordIsEmpty(String typeString) throws TomException:
{
  Token name;
  LinkedList blockList = new LinkedList();
  TargetLanguage tlCode;
  Option orgTrack;
}
{
  <TOM_IS_EMPTY>
    { orgTrack = ast().makeOriginTracking("is_empty", getLine(), currentFile);}
  <TOM_LPAREN> name = <TOM_IDENTIFIER> <TOM_RPAREN>
  tlCode = GoalLanguageBlock(blockList)
   {
     Option info = ast().makeOriginTracking(name.image,getLine(), currentFile);
     OptionList option = ast().makeOption(info);
     return tsf().makeDeclaration_IsEmptyDecl(
                           ast().makeVariable(option,name.image,typeString),
                           ast().reworkTLCode(tlCode, pretty), orgTrack);
   }
}

Declaration KeywordGetElement(String typeString) throws TomException:
{
  Token name1, name2;
  LinkedList blockList = new LinkedList();
  TargetLanguage tlCode;
  Option orgTrack;
}
{
  <TOM_GET_ELEMENT>
    { orgTrack = ast().makeOriginTracking("get_element", getLine(), currentFile);}
  <TOM_LPAREN> name1 = <TOM_IDENTIFIER> <TOM_COMMA> name2 = <TOM_IDENTIFIER> <TOM_RPAREN>
  tlCode = GoalLanguageBlock(blockList)
   {
     Option info1 = ast().makeOriginTracking(name1.image,getLine(), currentFile);
     Option info2 = ast().makeOriginTracking(name2.image,getLine(), currentFile);
     OptionList option1 = ast().makeOption(info1);
     OptionList option2 = ast().makeOption(info2);
     return tsf().makeDeclaration_GetElementDecl(
                           ast().makeVariable(option1,name1.image,typeString),
                           ast().makeVariable(option2,name2.image,"int"),
                           ast().reworkTLCode(tlCode, pretty), orgTrack);
   }
}

Declaration KeywordGetSize(String typeString) throws TomException:
{
  Token name;
  LinkedList blockList = new LinkedList();
  TargetLanguage tlCode;
  Option orgTrack;
}
{
  <TOM_GET_SIZE>
    { orgTrack = ast().makeOriginTracking("get_size", getLine(), currentFile);}
  <TOM_LPAREN> name = <TOM_IDENTIFIER> <TOM_RPAREN>
  tlCode = GoalLanguageBlock(blockList)
   {
     Option info = ast().makeOriginTracking(name.image,getLine(), currentFile);
     OptionList option = ast().makeOption(info);
     return tsf().makeDeclaration_GetSizeDecl(
                           ast().makeVariable(option,name.image,typeString),
                           ast().reworkTLCode(tlCode, pretty), orgTrack);
   }
}

TargetLanguage KeywordFsym() throws TomException:
{
  LinkedList blockList = new LinkedList();
  TargetLanguage tlCode;
}
{
  <TOM_FSYM>
  tlCode = GoalLanguageBlock(blockList)
   {
     return ast().reworkTLCode(tlCode, pretty);
   }
}

Declaration KeywordIsFsym(TomName astName, String typeString) throws TomException:
{
  Token name;
  LinkedList blockList = new LinkedList();
  TargetLanguage tlCode;
  Option orgTrack;
}
{
  <TOM_IS_FSYM>
    { orgTrack = ast().makeOriginTracking("is_fsym", getLine(), currentFile);}
  <TOM_LPAREN> name = <TOM_IDENTIFIER> <TOM_RPAREN>
  tlCode = GoalLanguageBlock(blockList)
   {
     Option info = ast().makeOriginTracking(name.image,getLine(), currentFile);
     OptionList option = ast().makeOption(info);
     return tsf().makeDeclaration_IsFsymDecl(
			   astName,
                           ast().makeVariable(option,name.image,typeString),
                           ast().reworkTLCode(tlCode, pretty), orgTrack);
   }
}


Declaration KeywordGetSlot(TomName astName, String typeString) throws TomException:
{
  Token name, slotName;
  LinkedList blockList = new LinkedList();
  TargetLanguage tlCode;
  Option orgTrack;
}
{
  <TOM_GET_SLOT>
    { orgTrack = ast().makeOriginTracking("get_slot", getLine(), currentFile);}
  <TOM_LPAREN> slotName = <TOM_IDENTIFIER>
     <TOM_COMMA> name = <TOM_IDENTIFIER> <TOM_RPAREN>
  tlCode = GoalLanguageBlock(blockList)
   {
       Option info = ast().makeOriginTracking(name.image,getLine(), currentFile);
       OptionList option = ast().makeOption(info);
       return tsf().makeDeclaration_GetSlotDecl(
         astName,
         tsf().makeTomName_Name(slotName.image),
         ast().makeVariable(option,name.image,typeString),
         ast().reworkTLCode(tlCode, pretty), orgTrack);
   }
}

Declaration KeywordMake(String opname, TomType returnType, TomTypeList types) throws TomException:
{
  Token typeArg;
  Token nameArg;
  LinkedList args = new LinkedList();
  LinkedList blockList = new LinkedList();
  TargetLanguage tlCode;
  int index = 0;
  TomType type;
  TomName name;
  Option orgTrack;
  int nbTypes = types.getLength();
}
{
  <TOM_MAKE>
     { orgTrack = ast().makeOriginTracking("make", getLine(), currentFile);}
     [ ( LOOKAHEAD(2) 
        <TOM_LPAREN> <TOM_RPAREN>
       |
        <TOM_LPAREN>
      nameArg = <TOM_IDENTIFIER>
       {
          if( !(nbTypes > 0) ) {
            type = tsf().makeTomType_EmptyType();
          } else {
	        type = (TomType)types.elementAt(index++);
	      }
	      name = tsf().makeTomName_Name(nameArg.image);  
	      Option info1 = ast().makeOriginTracking(nameArg.image,getLine(), currentFile);
	      OptionList option1 = ast().makeOption(info1);
	      args.add(tsf().makeTomTerm_Variable(option1,name, type));
       }
     ( <TOM_COMMA> nameArg = <TOM_IDENTIFIER>
        {
          if( index >= nbTypes ) {
            type = tsf().makeTomType_EmptyType();
          } else {
	        type = (TomType)types.elementAt(index++);
	      }
    	  name = tsf().makeTomName_Name(nameArg.image); 
   	      Option info2 = ast().makeOriginTracking(nameArg.image,getLine(), currentFile);
          OptionList option2 = ast().makeOption(info2);
          args.add(tsf().makeTomTerm_Variable(option2,name, type));
        }
      )*
      <TOM_RPAREN> ) ]

  tlCode = GoalLanguageBlock(blockList)
   {
     return ast().makeMakeDecl(opname,returnType,args,ast().reworkTLCode(tlCode, pretty), orgTrack);
   }
}

Declaration KeywordMakeEmptyList(String name) throws TomException:
{
  LinkedList blockList = new LinkedList();
  TargetLanguage tlCode;
  Option orgTrack;
}
{
  <TOM_MAKE_EMPTY>
    { orgTrack = ast().makeOriginTracking("make_empty", getLine(), currentFile);}
  [<TOM_LPAREN> <TOM_RPAREN>]
  tlCode = GoalLanguageBlock(blockList)
   {
     return tsf().makeDeclaration_MakeEmptyList(
                           tsf().makeTomName_Name(name),
                           ast().reworkTLCode(tlCode, pretty), orgTrack);
   }
}

Declaration KeywordMakeAddList(String name, String listType, String elementType) throws TomException:
{
  Token listName, elementName;
  LinkedList blockList = new LinkedList();
  TargetLanguage tlCode;
  Option orgTrack;
}
{
  <TOM_MAKE_INSERT>
    { orgTrack = ast().makeOriginTracking("make_add", getLine(), currentFile);}
  <TOM_LPAREN> elementName=<TOM_IDENTIFIER> <TOM_COMMA>
                                    listName=<TOM_IDENTIFIER> <TOM_RPAREN>
  tlCode = GoalLanguageBlock(blockList)
   {
     Option listInfo = ast().makeOriginTracking(listName.image,getLine(), currentFile);
     Option elementInfo = ast().makeOriginTracking(elementName.image,getLine(), currentFile);
     OptionList listOption = ast().makeOption(listInfo);
     OptionList elementOption = ast().makeOption(elementInfo);
     return tsf().makeDeclaration_MakeAddList(
       tsf().makeTomName_Name(name),
       ast().makeVariable(elementOption,elementName.image,elementType),
       ast().makeVariable(listOption,listName.image,listType),
       ast().reworkTLCode(tlCode, pretty), orgTrack);
   }
}

Declaration KeywordMakeEmptyArray(String name, String listType) throws TomException:
{
  Token listName;
  LinkedList blockList = new LinkedList();
  TargetLanguage tlCode;
  Option orgTrack;
}
{
  <TOM_MAKE_EMPTY>
    { orgTrack = ast().makeOriginTracking("make_empty", getLine(), currentFile);}
  <TOM_LPAREN> listName=<TOM_IDENTIFIER> <TOM_RPAREN>
  tlCode = GoalLanguageBlock(blockList)
   {
     Option listInfo = ast().makeOriginTracking(listName.image,getLine(), currentFile);
     OptionList listOption = ast().makeOption(listInfo);
     return tsf().makeDeclaration_MakeEmptyArray(
                           tsf().makeTomName_Name(name),
                           ast().makeVariable(listOption,listName.image,listType),
                           ast().reworkTLCode(tlCode, pretty), orgTrack);
   }
}

Declaration KeywordMakeAddArray(String name, String listType, String elementType) throws TomException:
{
  Token listName, elementName, positionName;
  LinkedList blockList = new LinkedList();
  TargetLanguage tlCode;
  Option orgTrack;
}
{
  <TOM_MAKE_APPEND>
    { orgTrack = ast().makeOriginTracking("make_append", getLine(), currentFile);}
  <TOM_LPAREN> elementName=<TOM_IDENTIFIER>    <TOM_COMMA>
                                    listName=<TOM_IDENTIFIER>
                 <TOM_RPAREN>
  tlCode = GoalLanguageBlock(blockList)
   {
     Option listInfo = ast().makeOriginTracking(listName.image,getLine(), currentFile);
     Option elementInfo = ast().makeOriginTracking(elementName.image,getLine(), currentFile);
     OptionList listOption = ast().makeOption(listInfo);
     OptionList elementOption = ast().makeOption(elementInfo);
     return tsf().makeDeclaration_MakeAddArray(
       tsf().makeTomName_Name(name),
       ast().makeVariable(elementOption,elementName.image,elementType),
       ast().makeVariable(listOption,listName.image,listType),
       ast().reworkTLCode(tlCode, pretty), orgTrack);
   }
}

