module sequents
imports String int 

abstract syntax

Term = Var(name:String)
     | funAppl(fun:String, p:TermList)
     | NewVar(name:String, base_name: String) // asks for a new term 
     | FreshVar(name:String, base_name: String) // asks for a fresh var

TermList = concTerm(Term*)

StringList = strlist(String*)

Prop = relationAppl(r:String, t:TermList)
     | and(p1:Prop, p2:Prop)
     | or(p1:Prop, p2:Prop)
     | implies(p1:Prop, p2:Prop)
     | forall(var:String, p:Prop)
     | exists(var:String, p:Prop)
     | bottom()
     | top()
     | nullProp() // for open leaves




Context = context(Prop*)

Sequent = sequent(h:Context,c:Context)

Premisses = premisses(Tree*)

// st == symbol table
Tree = rule(type:RuleType, p:Premisses, c:Sequent, active:Prop)
     | nullTree() // for conveniance

RuleType = axiomInfo()
         | impliesLeftInfo()
         | impliesRightInfo()
         | andLeftInfo()
         | andRightInfo()
         | orLeftInfo()
         | orRightInfo()
         | forallRightInfo(freshvar:Term)
         | forallLeftInfo(newterm:Term)
         | existsRightInfo(newterm:Term)
         | existsLeftInfo(freshvar:Term)
         | bottomInfo()
         | topInfo()
         | cutInfo(p:Prop)
         | contractionLeftInfo()
         | contractionRightInfo()
         | weakLeftInfo()
         | weakRightInfo()
         | openInfo() // open leaf
         | reductionInfo() // reduction step
         | customRuleInfo(name:String,expanded:Tree)

//--- new rules ---
SeqList = concSeq(Sequent*)

VarList = varlist(String*)

// hs == hand-side : 0 left, 1 right
Rule = ruledesc(hs:int, concl:Prop, prem:SeqList, tree:Tree)

TermRule = termrule(lhs:Term,rh:Term)
TermRuleList = termrulelist(TermRule*)
PropRule = proprule(lhs:Prop,rhs:Prop)
PropRuleList = proprulelist(PropRule*)

// --- for the new rules computation---
RuleList = rlist(Rule*)


// parsed in interactive mode
Command = rewriteprop(l:Prop, r:Prop)
        | rewritepropFromTheorem(th:String)
        | rewritesuper(l:Prop, r:Prop)
        | rewritesuperFromTheorem(th:String)
        | rewriteterm(lt:Term, rt:Term)
        | rewritetermFromTheorem(th:String)
        | normalizeProp(p: Prop)
        | normalizeTerm(t: Term)
        | proof(name:String, p:Prop)
        | display(name:String)
        | proofterm(name:String)
        | quit()
        | reinit()
        | proofcheck(name:String)
        | print(name:String)
        | resume(text:String)
        | gibber()
        | importfile(name:String)
        | inductiver(s:Sig)
        | inductive(s:Sig)
        | endoffile()
	
ProofCommand = proofCommand(text:String)
             | focusCommand(hyp:String)
             | cutCommand(p:Prop)
             | ruleCommand(n:int)
             | askrulesCommand()
             | theoremCommand(name:String)
             | normalizeSequent()
             | proofquit()
             | proofreinit()
             | abort()
             | proofendoffile()

/* --- stagiaires --- */
Sig = sig(defined:String,l:Ctorlist)
Ctorlist = clist(Ctor*)
Ctor = ctor(s:String, tl:TypeList)
TypeList = tlist(Type*)
Type = type(label:String,name:String)
/* ------------------ */


