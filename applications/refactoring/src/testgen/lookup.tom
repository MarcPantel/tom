%op Strategy Lookup(res:PositionWrapper) {
  make(res) {
    `IfThenElse(Is_Name(),LookupName(res),IfThenElse(Is_ConsDot(),LookupComposedName(res),Identity()))
  }
}


%op Strategy LookupComposedName(res:PositionWrapper) {
  make(res) { `Mu(MuVar("x"),IfThenElse(_ConsDot(Identity(),_EmptyDot()),
        _ConsDot(LookupName(res),Identity()),
        _ConsDot(Identity(),MuVar("x"))))  }
}


%strategy LookupName(res:PositionWrapper) extends Identity() {
  visit Name {
    Name[name=name] -> {
      `LookupAll(res,name).visit(getEnvironment());
    }
  }
}

%op Strategy TypeLookup(res:PositionWrapper) {
  make(res) { `Sequence(
      Lookup(res),
      ApplyAtPosition(res,IfThenElse(Is_FieldDecl(),_FieldDecl(Lookup(res),Identity(),Identity()),Identity())))
  }
}


%op Strategy LookupAll(res:PositionWrapper,name:String) {
  make(res,name) { `IfThenElse(
      onTheRightOfDot(),
      Choice(Up(Up(_ConsDot(TypeLookup(res),Identity()))),ApplyAtPosition(res,LookupAllMembers(res,FindName(res,name)))),
      Sequence(LookupAllDecls(res,FindName(res,name)),LookupAllPackageNodes(res,FindName(res,name)))) }
}


%strategy FindName(res:PositionWrapper, n:String) extends Identity() {
  visit ClassDecl {
    ClassDecl[name=Name[name=name]] -> {
      //System.out.println("try to compare the name "+n);
      //System.out.println("with "+`name);
      if (`name.equals(n)) {
        //System.out.println("must fail");
        res.value = getPosition();
        throw new VisitFailure();
      }
    }
  }
  visit PackageNode {
    PackageNode[packageName=Name[name=name]] -> {
      if (`name.equals(n)) {
        res.value = getPosition();
        throw new VisitFailure();
      }
    }
  }
  visit Stmt {
    LocalVariableDecl[name=Name[name=name]] -> {
      if (`name.equals(n)) {
        res.value = getPosition();
        throw new VisitFailure();
      }
    }
  }
  visit BodyDecl {
    FieldDecl[name=Name[name=name]] -> {
      if (`name.equals(n)) {
        res.value = getPosition();
        throw new VisitFailure();
      }
    }
  }
}

%op Strategy onTheRightOfDot() {
  make() { `Up(Sequence(Is_ConsDot(),Up(Is_ConsDot()))) }
}

%op Strategy LookupAllMembers(pos:PositionWrapper,s:Strategy) {
  make(pos,s) { `MuFixPoint("lookupAllMembers",MuVar("x"),
      IfThenElse(Is_ClassDecl(),
        Sequence(
          _ClassDecl(Identity(),Identity(),_ConcBodyDecl(IfThenElse(Is_FieldDecl(),s,IfThenElse(Is_MemberClassDecl(),_MemberClassDecl(s),Identity())))),
          Choice(_ClassDecl(Identity(),Lookup(pos),Identity()),ApplyAtPosition(pos,MuVar("x")))),
        IfThenElse(Is_PackageNode(),_PackageNode(Identity(),_ConcClassDecl(s)),Identity())))
  }
}

/**
%op Strategy LookupAllDecls(pos:PositionWrapper, s:Strategy) {
  make(pos,s) { 
    `Mu(MuVar("x"),
        Sequence(Debug("start LookupAllDecls"), 
          IfThenElse(Is_ClassDecl(),
            Sequence(
              Debug("_ClassDecl: test with this class"),
              s,
              Debug("_ClassDecl: lookup all members"),
              LookupAllMembers(pos,s),
              Debug("_ClassDecl: lookup at enclosing scope"),
              ApplyAtEnclosingScope(MuVar("x"))),
            IfThenElse(Is_PackageNode(),
              Sequence(Debug("_PackageNode: lookup all members"),
                LookupAllMembers(pos,s)),
              Sequence(
                Debug("Default case: apply at predecessors"),
                ApplyAtEnclosingStmt(ApplyToPredecessors(s)),
                Debug("Default case: apply at enclosing scope"),
                ApplyAtEnclosingScope(MuVar("x")))
              ))))
  }
}
*/

%op Strategy LookupAllDecls(pos:PositionWrapper, s:Strategy) {
  make(pos,s) { 
    `Mu(MuVar("x"),
        IfThenElse(Is_ClassDecl(),
          Sequence(
            s,
            LookupAllMembers(pos,s),
            ApplyAtEnclosingScope(MuVar("x"))),
          IfThenElse(Is_PackageNode(),
            LookupAllMembers(pos,s),
            Sequence(
              ApplyAtEnclosingStmt(ApplyToPredecessors(s)),
              ApplyAtEnclosingScope(MuVar("x")))
            )))
  }
}

static PositionWrapper root = new PositionWrapper(new Position());

%op Strategy LookupAllPackageNodes(pos:PositionWrapper,s:Strategy) {
  make(pos,s) { `ApplyAtPosition(root,_Prog(s)) }
}
