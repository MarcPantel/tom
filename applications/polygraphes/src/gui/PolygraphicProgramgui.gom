//Gom File for all polygraphic programs
//Caution : this is a quite complete grammar for polygraphic programs though most of the things concerning 3-paths are actually never used

module adt.PolygraphicProgramgui
	imports int String
	abstract syntax

OnePath = Id() //means null, empty
		| OneCell (Name:String,x:int,y:int,hauteur:int,largeur:int)
		| OneC0 (OnePath*)

	//IDs were added for a future purpose though they are not necessary to do the calculations : 
TwoPath = TwoId (onePath:OnePath) //to be able to cast wires as cells when needed
		| TwoCell (Name:String,Source:OnePath,Target:OnePath,Type:CellType,ID:int,x:int,y:int,hauteur:int,largeur:int)
		| TwoC0 (TwoPath*)
		| TwoC1 (TwoPath*)

ThreePath = ThreeId (twoPath:TwoPath)//useless for the moment
| ThreeCell (Name:String,Source:TwoPath,Target:TwoPath,Type:CellType)
| ThreeC0 (ThreePath*)//useless
| ThreeC1 (ThreePath*)//useless
| ThreeC2 (ThreePath*)//useless
		
CellType = Constructor()
		| Function()

		
//HOOKS on variadic operators		

//ONEC0

//defines variadic operator OneC0 as associative and unitary with neutral element Id() 
OneC0:AU() { `Id() }


//TWOC0

//desactivates the default hook
TwoC0:Free() {}

//defines how to create an empty TwoC0
TwoC0:make_empty() { return `TwoId(Id()); }

//defines variadic operator TwoC0 as associative and unitary with neutral element TwoId(Id()) 
TwoC0:make_insert(x,y) {
%match(x,y){
	TwoId(Id()),tail -> {return `tail; }
	head,TwoId(Id())  -> {return `head; }
	TwoC0(h,t),tail -> {return `TwoC0(h,TwoC0(t,tail)); }
	TwoC1(),g -> { return `g; }
	g,TwoC1() -> { return `g; }
	}
}

//TWOC1

//desactivates the default hook
TwoC1:Free() {}

//defines how to create an empty TwoC0
TwoC1:make_empty() { return `TwoId(Id()); }

//defines variadic operator TwoC0 as associative and unitary with neutral element TwoId(Id()) 
TwoC1:make_insert(x,y) {
	//first we test if x and y are compatible for the *1 association
	/*if(y!=`TwoId(Id())&&x.target()!=y.source())
	{	System.out.println("x : "+x);System.out.println("x.target : "+x.target());
		System.out.println("y : "+y);System.out.println("y.target : "+y.source());
		throw new RuntimeException("composition of incompatible 2-Paths");
	}*/
%match(x,y){
	TwoId(Id()),tail -> {return `tail; }
	head,TwoId(Id())  -> {return `head; }
	TwoC1(h,t),tail -> {return `TwoC1(h,TwoC1(t,tail)); }
	TwoC0(),g -> { return `g; }
	g,TwoC0() -> { return `g; }
	}
}

//METHODS for polygraphic terms

//set of methods for 1-paths
sort OnePath:block(){
	
	//returns true if the path is valid (=well-constructed) (always true for a 1-path)
	//actually useless with our custom make methods
	public boolean defined(){return true;}
	
	
	public int getLargeur(){
		%match (this){
			OneCell[largeur=x] -> { return `x; }
			OneC0 (head,tail*) -> { return `head.getLargeur()+4+`tail*.getLargeur();} 
		}
		return 0;
	}
	
	public int getHauteur(){
		%match (this){
			OneCell[hauteur=x] -> { return `x; }
			OneC0 (head,tail*) -> { return `head.getHauteur();} 
		}
		return 0;
	}
	
	public int getX(){
		%match (this){
			OneCell[x=x] -> { return `x; }
			OneC0 (head,tail*) -> {return `head.getX();} 
		}
		return 0;
	}
	
	public int getY(){
		%match (this){
			OneCell[y=x] -> { return `x; }
			OneC0 (head,tail*) -> {return `head.getY();} 
		}
		return 0;
	}
	
	//methods to print 1-path in a more "readable" way
	//top level
	public String prettyPrint(){
		%match (this){
			OneC0(left,right*) -> { return "OneC0("+`left.prettyPrint()+","+`OneC0(right*).prettyPrint()+")";}
		}
		return this.toString();
	}
	//other levels
	public String prettyPrintBis(){
		%match (this){
			o@OneCell(_,_,_,_,_) -> { return `o.getName(); }
			OneC0(left,right*) -> { return `left.prettyPrintBis()+","+`OneC0(right*).prettyPrintBis();}
		}
		return this.toString();
	}
	//called method
	public void print(){System.out.println(this.prettyPrint());}
}

//set of methods for 2-paths
sort TwoPath:block(){
	
	public int getX(){
		%match (this){
			TwoId(X) -> { return `X.getX(); }
			TwoCell[x=x] -> { return `x; }
			TwoC0(head,tail*) -> { return `head.getX(); }
			TwoC1(head,tail*) -> { return `head.getX();}
		}
		throw new RuntimeException("strange term: "+this);
	}
	
	public int getY(){
		%match (this){
			TwoId(X) -> { return `X.getY(); }
			TwoCell[y=x] -> { return `x; }
			TwoC0(head,tail*) -> { return `head.getY(); }
			TwoC1(head,tail*) -> { `head.getY();}
		}
		throw new RuntimeException("strange term: "+this);
	}
	
	public int getLargeur(){
		%match (this){
			TwoId(X) -> { return `X.getLargeur(); }
			TwoCell[largeur=x] -> { return `x; }
			TwoC0(head,tail*) -> { return `head.getLargeur()+4+`tail*.getLargeur(); }
			TwoC1(head,tail*) -> { 
				if(`head.getLargeur()>`tail*.getLargeur()){
					return `head.getLargeur();
				}else{
					return `tail*.getLargeur();
				}
			
			}
		}
		throw new RuntimeException("strange term: "+this);
	}
	
	public int getHauteur(){
		%match (this){
			TwoId(X) -> { return `X.getHauteur(); }
			TwoCell[hauteur=x] -> { return `x; }
			TwoC0(head,tail*) -> { 
				if(`head.getHauteur()>`tail*.getHauteur()){
					return `head.getHauteur();
			   }else{
				    return `tail*.getHauteur();
			   }
			}
			TwoC1(head,tail*) -> { return `head.getHauteur()+4+`tail*.getHauteur(); }
		}
		throw new RuntimeException("strange term: "+this);
	}
	
	//returns the source 1-Path of any 2-Path
	public OnePath source(){
		%match (this){
			TwoId(X) -> { return `X; }
			TwoCell[Source=x] -> { return `x; }
			TwoC0(head,tail*) -> { return `OneC0(head.source(),tail*.source()); }
			TwoC1(head,tail*) -> { return `head.source(); }
		}
		//return `OneId();//actually useless
		throw new RuntimeException("strange term: "+this);
	}

	//returns the target 1-Path of any 2-Path
	public OnePath target(){
		%match (this){
			TwoId(X) -> { return `X; }
			TwoCell[Target=x] -> { return `x; }
			TwoC0(head,tail*) -> { return `OneC0(head.target(),tail*.target()); }
			TwoC1(head*,tail) -> { return `tail.target(); }
		}
		//return `OneId();//actually useless
		throw new RuntimeException("strange term: "+this);
	}
	
	//returns true if the path is valid (=well-constructed)
	//actually useless with our custom make methods
	public boolean defined(){
		%match (this){
			TwoCell[] -> { return true; }
			TwoC0(_*) -> { return true; }
			TwoC1(head, tail*) -> { return `head.target()==`tail.source()&&`tail.defined();}
		}
		return false;
		}
	
	//return the size of the source defined as the number of 1-Cells in the input
	public int sourcesize(){
		OnePath source=this.source();
		%match(source){
			Id() -> { return 0; }
			OneCell(_,_,_,_,_) -> { return 1; }
			o@OneC0(_*) -> { return `o.length(); }
		}
		return 0;
	}
	
	//return the size of the target defined as the number of 1-Cells in the output
	public int targetsize(){
		OnePath target=this.target();
		%match(target){
			Id() -> { return 0; }
			OneCell(_,_,_,_,_) -> { return 1; }
			o@OneC0(_*) -> { return `o.length(); }
		}
		return 0;
	}
	
	public void affiche(){
		%match (this){
			TwoId(X) -> { System.out.println(); }
			t@TwoCell(_,_,_,_,_,_,_,_,_) -> { System.out.println(`t.getName()+":: x :"+`t.getx()+" y :"+`t.gety()+" largeur :"+`t.getlargeur()+" hauteur :"+`t.gethauteur());}
			TwoC0(head,tail*) -> { `head.affiche(); `tail*.affiche(); }
			TwoC1(head,tail*) -> { `head.affiche(); `tail*.affiche(); }
		}
	}
	
	//methods to print 2-path in a more "readable" way
	//top level
	public  String prettyPrint (){
		%match (this){
			TwoC0(left,right*) -> { return "TwoC0("+`left.prettyPrint()+","+`TwoC0(right*).prettyPrint()+")";}
			TwoC1(left,right*) -> { return "TwoC1("+`left.prettyPrint()+","+`TwoC1(right*).prettyPrint()+")";}
		}
		return this.prettyPrintBis();
		}
	//other levels
	public  String prettyPrintBis(){
		%match (this){
			t@TwoCell(_,_,_,_,_,_,_,_,_) -> { return `t.getName(); }
			TwoId(o@OneCell(_,_,_,_,_)) -> { return `o.prettyPrintBis(); }
			TwoC0(left,right*) -> { return `left.prettyPrintBis()+","+`TwoC0(right*).prettyPrintBis();}
		}
		return this.toString();
		}
	//called method
	public void print(){System.out.println(this.prettyPrint());}
}

//set of methods for 3-paths (never used in this project except for the print method)
sort ThreePath:block(){
	
	//returns the source 1-Path of any 3-Path (same 1-source and 1-target for the source and the target of a 3-Cell) 
	public OnePath oneSource(){
		%match (this){	
					ThreeId(X) -> { return `X.source(); }
					ThreeCell[Source=x] -> { return `x.source(); }
					ThreeC0(head,tail*) -> { return `OneC0(head.oneSource(),tail*.oneSource()); }
					ThreeC1(head,tail*) -> { return `head.oneSource(); }
					ThreeC2(head,tail*) -> { return `head.oneSource(); }
		}
		//return `OneId();
		throw new RuntimeException("strange term: "+this);
	}

	//returns the source 1-Path of any 3-Path (same 1-source and 1-target for the source and the target of a 3-Cell) 
	public OnePath oneTarget(){
		%match (this){
					ThreeId(X) -> { return `X.target(); }
					ThreeCell[Target=x] -> { return `x.target(); }
					ThreeC0(head,tail*) -> { return `OneC0(head.oneTarget(),tail*.oneTarget()); }
					ThreeC1(head*,tail) -> { return `tail.oneTarget(); }
					ThreeC2(head,tail*) -> { return `head.oneTarget(); }
		}
		//return `OneId();
		throw new RuntimeException("strange term: "+this);
	}
	
	//returns the source 2-Path of any 3-Path
	public TwoPath source(){
		%match (this){
					ThreeId(X) -> { return `X; }
					ThreeCell[Source=x] -> { return `x; }
					ThreeC0(head,tail*) -> { return `TwoC0(head.source(),tail*.source()); }
					ThreeC1(head,tail*) -> { return `TwoC1(head.source(),tail*.source()); }
					ThreeC2(head,tail*) -> { return `head.source(); }
		}
		//return `TwoId(OneId());
		throw new RuntimeException("strange term: "+this);
	}

	//returns the target 2-Path of any 3-Path
	public TwoPath target(){
		%match (this){
					ThreeId(X) -> { return `X; }
					ThreeCell[Target=x] -> { return `x; }
					ThreeC0(head,tail*) -> { return `TwoC0(head.target(),tail*.target()); }
					ThreeC1(head,tail*) -> { return `TwoC1(head.target(),tail*.target()); }
					ThreeC2(head*,tail) -> { return `tail.target(); }
		}
		//return `TwoId(OneId());
		throw new RuntimeException("strange term: "+this);
	}
	
	//returns true if the path is valid (=well-constructed)
	//actually useless with our custom make methods
	public boolean defined(){
			%match (this){
						ThreeCell[] -> { return true; }
						ThreeC0(_*) -> { return true; }
						ThreeC1(ThreeCell[Source=source1],ThreeCell[Source=source2],tail*) -> { return `source1.target()==`source2.source()&&`tail.defined();}
						ThreeC2(ThreeCell[Target=target1],ThreeCell[Source=source2],tail*) -> { return `target1==`source2&&`tail.defined();}
				}
			return false;
		}
	
	//print method
	public void print(){System.out.println("ThreeCell("+this.getName()+","+this.getSource().prettyPrint()+","+this.getTarget().prettyPrint()+")");}
		
}