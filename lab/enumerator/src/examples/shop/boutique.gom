module examples.shop.Boutique
imports int
abstract syntax

LineItem = lineItem(item:Item, quantity:int)

Item = item(id:int, price:int)

Inventory = items(LineItem*)

//ShoppingCart = lines(LineItem*)

Carts = carts(Inventory*)
Shop = shop(inventory:Inventory, carts:Carts)


// inventories should contain only one line per item
items:make_insert(e,l) {
  %match(LineItem e, Inventory l) {
    lineItem(item(id,p), q), items(IL1*, lineItem(item(id,p2),q2), IL2*) -> { 
      return `items(IL1*, lineItem(item(id,p),q+q2), IL2*); 
    }
  }
  return realMake(e,l);
}


// carts should only contain items from the inventory and in less quantity
// items should have same price in carts and inventory
shop:make(i,cs) {
  //   examples.shop.boutique.types.Inventory inv = examples.shop.boutique.types.Shop.fix(i,cs);
  %match(Carts cs, Inventory i) {
    scarts@carts(C1*, items(I1*, it@lineItem(item(id,p),q), I2*), C2*),
           !items(IL1*, lineItem(item(id,_),_), IL2*) -> {
      return `shop(items(it,i*), scarts ); 
    }
    
    scarts@carts(C1*, items(I1*, lineItem(item(id,p),q), I2*), C2*),
           items(IL1*, lineItem(item(id,pi),qi), IL2*) -> {
      if(`qi<`q){
        return `shop( items(IL1*, lineItem(item(id,p),qi+q), IL2*), scarts );
      }else if(`pi!=`p){
        return `shop( items(IL1*, lineItem(item(id,p),qi), IL2*), scarts );
      }
    }
  }
  return realMake(i,cs);
}

