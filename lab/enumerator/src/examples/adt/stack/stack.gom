module examples.adt.stack.Stack
imports int
abstract syntax

Stack = empty()
      | push(stack:Stack, element:int)


sort Stack:block() {

  public Stack empty() {
    return `empty();
  }

  public Stack push(Integer element) {
    return `push(this, element);
  }

  public boolean isEmpty() {
    return this.isempty();
  }

  public Integer top() throws examples.adt.stack.EmptyStackException {
    %match(this) {
      push(_, e) -> { return `e; }
    }
    throw new examples.adt.stack.EmptyStackException();
  }

  public Stack pop() throws examples.adt.stack.EmptyStackException {
    %match(this) {
      push(s, _) -> { return `s; }
    }
    throw new examples.adt.stack.EmptyStackException();
  }

  public  int size() {
    %match(this) {
      empty()    -> { return 0; }
      push(s, _) -> { return 1 + `s.size(); } 
    }
    throw new RuntimeException("bad term " + this);
  }

}


sort Stack:interface() { examples.adt.stack.IStack }

